# Maintainer: PaxPlay <pax@paxplay.me>

pkgname=spcomp
pkgver=1.10
pkgrel=1
pkgdesc='A small, statically typed scripting language.'
url='https://github.com/alliedmodders/sourcepawn'
arch=('any')
license=('none')
depends=('lib32-glibc')
makedepends=('python-ambuild')
_commit='e8d7b67b0665baf1faf6d4985c1e92ac1163e43f'
_amtlcommit='d0b8c6a1c8d30e5c50093de1cce63255faa904ef'
source=("https://github.com/alliedmodders/sourcepawn/archive/$_commit.tar.gz"
        "https://github.com/alliedmodders/amtl/archive/$_amtlcommit.tar.gz")
sha256sums=('4fa01addee5ddba14037f8ac270c4611a3652223d9e8bac7af81932143c2ead6'
            '58356b4086a8b7aa1abd95b27310181950a29a50007eb73660c88b04962df4c6')

prepare() {
  mkdir -p "$srcdir/sourcepawn-$_commit/build"
}

build() {
  cd "$srcdir/sourcepawn-$_commit/build" || exit 1
  rm -r "$srcdir/sourcepawn-$_commit/third_party/amtl"
  ln -s "$srcdir/amtl-$_amtlcommit" "$srcdir/sourcepawn-$_commit/third_party/amtl" # move amtl over, for some reason giving a seperate path to ambuild is broken
  #python ../configure.py --build spcomp --amtl "$srcdir/amtl-$_amtlcommit"
  python ../configure.py --build spcomp
  ambuild
}

package() {
  mkdir -p "$pkgdir/usr"
  mkdir -p "$pkgdir/usr/bin"
  cp "$srcdir/sourcepawn-$_commit/build/compiler/spcomp/linux-x86_64/spcomp" "$pkgdir/usr/bin/spcomp"
}

